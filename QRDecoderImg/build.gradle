plugins {
    id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    // ZXING para decodificación de QR
    implementation libs.core
    implementation libs.javase

    // JSON para estructurar la respuesta
    implementation libs.json

    // Librerías de Kony Middleware (Cargadas desde la carpeta lib/)
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    // Dependencias de Test
    testImplementation libs.junit
}

test {
    useJUnit()
}

// Tarea para generar el Fat JAR (con todas las dependencias incluidas)
tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Genera un JAR ejecutable que contiene todas las dependencias.'
    
    archiveClassifier = 'all'
    
    manifest {
        attributes(
            'Main-Class': 'com.sybven.qrdecoder.Main'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    with jar
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
